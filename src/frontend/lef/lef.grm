%%

%name LEF

%term ID of string
	| REAL of real
	| STRING of string
	| VERSION
	| NAMESCASESENSITIVE
	| BUSBITCHARS
	| DIVIDERCHAR
	| UNITS
	| END
	| USEMINSPACING
	| OBS
	| PIN
	| ON
	| OFF
	| CLEARANCEMEASURE
	| MAXXY
	| EUCLIDEAN
	| MANUFACTURINGGRID
	| LAYER
	| TYPE
	| MASTERSLICE
	| CUT
	| ROUTING
	| IMPLANT
	| SPACING
	| BY
	| DIRECTION
	| HORIZONTAL
	| VERTICAL
	| PITCH
	| OFFSET
	| WIDTH
	| TO
	| TIME
	| CAPACITANCE
	| RESISTANCE
	| POWER
	| CURRENT
	| VOLTAGE
	| DATABASE
	| FREQUENCY
	| NANOSECONDS
	| PICOFARADS
	| OHMS
	| MILLIWATTS
	| MILLIAMPS
	| VOLTS
	| MICRONS
	| MEGAHERTZ
	| RPERSQ
	| CPERSQDIST
	| EDGECAPACITANCE
	| VIA
	| DEFAULT
	| RECT
	| POLYGON
	| SEMICOLON
	| VIARULE
	| GENERATE
	| OVERHANG
	| METALOVERHANG
	| SITE
	| CLASS
	| PAD
	| CORE
	| SYMMETRY
	| X
	| Y
	| SIZE
	| EOF

%nonterm START of LEFAST.t
	| STMTLIST of LEFAST.s list
	| STMT of LEFAST.s
	| VERSIONSTMT of LEFAST.s
	| DIVIDERCHARSTMT of LEFAST.s
	| BUSBITCHARSSTMT of LEFAST.s
	| NAMESCASESENSITIVESTMT of LEFAST.s
	| UNITSSTMT of LEFAST.s
	| UNITSLIST of LEFAST.u list
	| UNITSELEMENT of LEFAST.u
	| CLEARANCEMEASURESTMT of LEFAST.s
	| MANUFACTURINGGRIDSTMT of LEFAST.s
	| USEMINSPACINGSTMT of LEFAST.s
	| SWITCHONOFF of bool
	| LAYERSTMT of LEFAST.s
	| LAYERMASTERSLICE of LEFAST.s
	| LAYERCUT of LEFAST.s
	| LAYERROUTING of LEFAST.s
	| LAYERIMPLANT of LEFAST.s
	| DIR of LEFAST.d
	| VIASTMT of LEFAST.s
	| DRAWLAYERLIST of LEFAST.dl list
	| DRAWLAYER of LEFAST.dl
	| SHAPELIST of LEFAST.shape list
	| RECTSTMT of LEFAST.shape
	| POLYGONSTMT of LEFAST.shape
	| PTLIST of LEFAST.pt list
	| PT of LEFAST.pt
	| VIARULEGENERATESTMT of LEFAST.s
	| SITESTMT of LEFAST.s
	| CLASSSTMT of LEFAST.class
	| SIZESTMT of real * real
	| SYMMETRYSTMT of LEFAST.symmetry

%pos int

%keyword VERSION
	NAMESCASESENSITIVE
	BUSBITCHARS
	DIVIDERCHAR
	UNITS
	DATABASE
	MICRONS
	END
	USEMINSPACING
	OBS
	PIN
	ON
	OFF
	CLEARANCEMEASURE
	EUCLIDEAN
	MANUFACTURINGGRID
	LAYER
	TYPE
	MASTERSLICE
	CUT
	SPACING
	BY
	ROUTING
	DIRECTION
	HORIZONTAL
	VERTICAL
	PITCH
	OFFSET
	WIDTH
	TO
	RESISTANCE
	RPERSQ
	CAPACITANCE
	CPERSQDIST
	EDGECAPACITANCE
	VIA
	DEFAULT
	RECT
	POLYGON
	VIARULE
	GENERATE
	OVERHANG
	METALOVERHANG
	SITE
	CLASS
	PAD
	CORE
	SYMMETRY
	X
	Y
	SIZE

%start START
%eop EOF
%noshift EOF

%nodefault

%verbose

%%

START : STMTLIST (LEFAST.Top STMTLIST1)

STMTLIST : ([])
	| STMT STMTLIST (STMT1::STMTLIST1)

STMT :
	VERSIONSTMT (VERSIONSTMT1)
	| NAMESCASESENSITIVESTMT (NAMESCASESENSITIVESTMT1)
	| BUSBITCHARSSTMT (BUSBITCHARSSTMT1)
	| DIVIDERCHARSTMT (DIVIDERCHARSTMT1)
	| UNITSSTMT (UNITSSTMT1)
	| USEMINSPACINGSTMT (USEMINSPACINGSTMT1)
	| CLEARANCEMEASURESTMT (CLEARANCEMEASURESTMT1)
	| MANUFACTURINGGRIDSTMT (MANUFACTURINGGRIDSTMT1)
	| LAYERSTMT (LAYERSTMT1)
	| VIASTMT (VIASTMT1)
	| VIARULEGENERATESTMT (VIARULEGENERATESTMT1)
	| SITESTMT (SITESTMT1)

VERSIONSTMT : VERSION REAL SEMICOLON (LEFAST.VersionStmt REAL1)

NAMESCASESENSITIVESTMT :
	NAMESCASESENSITIVE SWITCHONOFF SEMICOLON (LEFAST.NamesCaseSensitiveStmt SWITCHONOFF1)

BUSBITCHARSSTMT :
	BUSBITCHARS STRING SEMICOLON (LEFAST.BusBitCharsStmt STRING1)

DIVIDERCHARSTMT :
	DIVIDERCHAR STRING SEMICOLON (LEFAST.DividerCharStmt STRING1)

UNITSSTMT :
	UNITS UNITSLIST END UNITS (LEFAST.UnitsStmt UNITSLIST1)

UNITSLIST : ([])
	| UNITSELEMENT UNITSLIST (UNITSELEMENT1::UNITSLIST1)

UNITSELEMENT :
	TIME NANOSECONDS REAL SEMICOLON (LEFAST.TimeNS REAL1)
	| CAPACITANCE PICOFARADS REAL SEMICOLON (LEFAST.CapacitancePF REAL1)
	| RESISTANCE OHMS REAL SEMICOLON (LEFAST.ResistanceOhms REAL1)
	| POWER MILLIWATTS REAL SEMICOLON (LEFAST.PowerMW REAL1)
	| CURRENT MILLIAMPS REAL SEMICOLON (LEFAST.CurrentMA REAL1)
	| VOLTAGE VOLTS REAL SEMICOLON (LEFAST.VoltageVolts REAL1)
	| DATABASE MICRONS REAL SEMICOLON (LEFAST.DatabaseMicrons REAL1)
	| FREQUENCY MEGAHERTZ REAL SEMICOLON (LEFAST.FreqMHz REAL1)

USEMINSPACINGSTMT :
	USEMINSPACING OBS SWITCHONOFF SEMICOLON (LEFAST.UseMinSpacingObsStmt SWITCHONOFF1)
	| USEMINSPACING PIN SWITCHONOFF SEMICOLON (LEFAST.UseMinSpacingPinStmt SWITCHONOFF1)

CLEARANCEMEASURESTMT :
	CLEARANCEMEASURE MAXXY SEMICOLON (LEFAST.ClearanceMeasureStmt LEFAST.MaxXY)
	| CLEARANCEMEASURE EUCLIDEAN SEMICOLON (LEFAST.ClearanceMeasureStmt LEFAST.Euclidean)

MANUFACTURINGGRIDSTMT :
	MANUFACTURINGGRID REAL SEMICOLON (LEFAST.ManufacturingGridStmt REAL1)

SWITCHONOFF :
	ON (true)
	| OFF (false)

LAYERSTMT :
	LAYERMASTERSLICE (LAYERMASTERSLICE1)
	| LAYERCUT (LAYERCUT1)
	| LAYERROUTING (LAYERROUTING1)
	| LAYERIMPLANT (LAYERIMPLANT1)

LAYERMASTERSLICE :
	LAYER ID
	TYPE MASTERSLICE SEMICOLON
	END ID
	(LEFAST.LayerMasterslice {name=ID1})

LAYERCUT :
	LAYER ID
	TYPE CUT SEMICOLON
	SPACING REAL SEMICOLON
	END ID
	(LEFAST.LayerCut {name=ID1,spacing=REAL1})

LAYERROUTING :
	LAYER ID
	TYPE ROUTING SEMICOLON
	DIRECTION DIR SEMICOLON
	PITCH REAL SEMICOLON
	OFFSET REAL SEMICOLON
	WIDTH REAL SEMICOLON
	SPACING REAL SEMICOLON
	RESISTANCE RPERSQ REAL SEMICOLON
	CAPACITANCE CPERSQDIST REAL SEMICOLON
	EDGECAPACITANCE REAL SEMICOLON
	END ID
	(LEFAST.LayerRouting
		{name=ID1,
		dir=DIR1,
		pitch=REAL1,
		offset=REAL2,
		width=REAL3,
		spacing=REAL4,
		res=REAL5,
		cap=REAL6,
		ecap=REAL7})

LAYERIMPLANT :
	LAYER ID
	TYPE IMPLANT SEMICOLON
	END ID
	(LEFAST.LayerImplant {name=ID1})

DIR :
	HORIZONTAL (LEFAST.Horizontal)
	| VERTICAL (LEFAST.Vertical)

VIASTMT :
	VIA ID DEFAULT
	DRAWLAYERLIST
	END ID
	(LEFAST.ViaStmt{name=ID1,layers=DRAWLAYERLIST1})

DRAWLAYERLIST : ([])
	| DRAWLAYER DRAWLAYERLIST (DRAWLAYER1::DRAWLAYERLIST1)

DRAWLAYER :
	LAYER ID SEMICOLON SHAPELIST (LEFAST.DrawLayer(ID1, SHAPELIST1))

SHAPELIST : ([])
	| RECTSTMT SHAPELIST (RECTSTMT1::SHAPELIST1)
	| POLYGONSTMT SHAPELIST (POLYGONSTMT1::SHAPELIST1)

RECTSTMT :
	RECT PT PT SEMICOLON (LEFAST.Rect (PT, PT))

POLYGONSTMT :
	POLYGON PTLIST SEMICOLON (LEFAST.Polygon PTLIST1)

PTLIST : ([])
	| PT PTLIST (PT1::PTLIST1)

PT :
	REAL REAL (LEFAST.Point (REAL1, REAL2))

VIARULEGENERATESTMT :
	VIARULE ID GENERATE
	LAYER ID SEMICOLON
		DIRECTION DIR SEMICOLON
		WIDTH REAL TO REAL SEMICOLON
		OVERHANG REAL SEMICOLON
		METALOVERHANG REAL SEMICOLON
	LAYER ID SEMICOLON
		DIRECTION DIR SEMICOLON
		WIDTH REAL TO REAL SEMICOLON
		OVERHANG REAL SEMICOLON
		METALOVERHANG REAL SEMICOLON
	LAYER ID SEMICOLON
		RECTSTMT
		SPACING REAL BY REAL SEMICOLON
	END ID
	(LEFAST.ViaRuleGenerateStmt
		{name=ID1,
		rlayer1={name=ID2,
				dir=DIR1,
				width=(REAL1,REAL2),
				overhang=REAL3,
				metaloverhang=REAL4},
		rlayer2={name=ID3,
				dir=DIR2,
				width=(REAL5,REAL6),
				overhang=REAL7,
				metaloverhang=REAL8},
		clayer={name=ID3,
				shp=RECTSTMT,
				spacing=(REAL9,REAL10)}})
	| VIARULE ID GENERATE
	LAYER ID SEMICOLON
		DIRECTION DIR SEMICOLON
	LAYER ID SEMICOLON
		DIRECTION DIR SEMICOLON
	END ID
	(LEFAST.ViaRuleGenerateTurnStmt
		{name=ID1,
		rlayer1={name=ID2,dir=DIR1},
		rlayer2={name=ID3,dir=DIR2}})

SITESTMT :
	SITE ID
	CLASSSTMT
	SYMMETRYSTMT
	SIZESTMT
	END ID
	(LEFAST.SiteStmt
		{name=ID1,cls=CLASSSTMT1,sym=SYMMETRYSTMT1,size=SIZESTMT1})

CLASSSTMT:
	CLASS PAD SEMICOLON (LEFAST.Pad)
	| CLASS CORE SEMICOLON (LEFAST.Core)

SYMMETRYSTMT :
	SYMMETRY X SEMICOLON (LEFAST.SymmX)
	| SYMMETRY Y SEMICOLON (LEFAST.SymmY)
	| SYMMETRY X Y SEMICOLON (LEFAST.SymmXY)

SIZESTMT :
	SIZE REAL BY REAL SEMICOLON ((REAL1,REAL2))

